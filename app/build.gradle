import com.poker.plugin.*

plugins {
    id 'com.poker.plugin'
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
    id 'dagger.hilt.android.plugin'
    id 'com.google.secrets_gradle_plugin' version "0.6"
}

static def gitVersionCode() {
    def cmd = 'git rev-list HEAD --first-parent --count'
    cmd.execute().text.trim().toInteger()
}

static def gitVersionTag() {
    def cmd = 'git describe --tags'
    def version = cmd.execute().text.trim()

    def pattern = "-(\\d+)-g"
    def matcher = version =~ pattern

    if (matcher) {
        version = version.substring(0, matcher.start()) + "." + matcher[0][1]
    }
    return version
}

android {
    compileSdkVersion BuildConfig.compileSdkVersion
    buildToolsVersion BuildConfig.buildToolsVersion

    defaultConfig {
        applicationId BuildConfig.applicationId
        minSdkVersion BuildConfig.minSdkVersion
        targetSdkVersion BuildConfig.targetSdkVersion
        versionCode BuildConfig.versionCode
        versionName BuildConfig.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride = gitVersionCode()
            output.versionNameOverride = gitVersionTag()
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding = true
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
    }
}

dependencies {
    implementation project(path: ':common')

    //Google firebase
    implementation platform('com.google.firebase:firebase-bom:28.0.0')
    implementation 'com.google.firebase:firebase-core:19.0.0'
    implementation 'com.google.firebase:firebase-common-ktx:20.0.0'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.android.gms:play-services-auth:19.2.0'
    implementation 'com.google.firebase:firebase-perf-ktx'
    implementation 'com.google.android.gms:play-services-maps:17.0.1'
    implementation 'com.google.android.gms:play-services-location:18.0.0'

    //阿里路由框架
    //Router
    implementation Deps.Router.arouterApi
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    kapt Deps.Router.arouterCompiler
    //hilt
    implementation Deps.Jetpack.Hilt.hiltAndroid
    kapt Deps.Jetpack.Hilt.hiltAndroidCompiler
    kapt Deps.Jetpack.Hilt.hiltCompiler
    //DataBase
    kapt Deps.Jetpack.Room.roomCompiler
    //二维码
    implementation Deps.Util.zxingLite
    //权限管理
    implementation Deps.Permissions.permissionsdispatcher
    implementation Deps.Permissions.ktx
    kapt Deps.Permissions.processor
    //评分
    implementation 'com.github.wsj1024:AndRatingBar:1.0.6'
    //banner
    implementation 'com.github.zhpanvip:BannerViewPager:3.5.0'
    //输入框
    implementation 'com.github.poovamraj:PinEditTextField:1.2.6'
}